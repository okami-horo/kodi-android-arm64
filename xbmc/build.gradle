apply plugin: 'com.android.application'

def keystoreEnvPath = System.getenv("KODI_ANDROID_STORE_FILE")
def keystoreAlias = System.getenv("KODI_ANDROID_KEY_ALIAS")
def keystorePassword = System.getenv("KODI_ANDROID_KEY_PASSWORD")
def keystoreStorePassword = System.getenv("KODI_ANDROID_STORE_PASSWORD")
def hasSigningEnv = [keystoreEnvPath, keystoreAlias, keystorePassword, keystoreStorePassword]
        .every { it != null && !it.isEmpty() }
def ndkHome = System.getenv("ANDROID_NDK_ROOT") ?: System.getenv("ANDROID_NDK_HOME")

android {
    namespace = 'org.xbmc.kodi'
    compileSdk = 36
    ndkVersion = "28.2.13676358"
    if (ndkHome) {
        ndkPath = ndkHome
    }
    defaultConfig {
        applicationId = "org.xbmc.kodi"
        minSdk = 24
        targetSdk = 36
        versionCode = 2190702
        versionName = "22.0-ALPHA2"
    }
    signingConfigs {
        if (hasSigningEnv) {
            release {
                keyAlias = keystoreAlias
                keyPassword = keystorePassword
                storeFile = file(keystoreEnvPath)
                storePassword = keystoreStorePassword
                enableV1Signing = true
                enableV2Signing = true
                enableV3Signing = true
            }
        }
    }
    buildTypes {
        debug {
            if (hasSigningEnv) {
                signingConfig = signingConfigs.release
            }
        }
        relwithdebinfo {
            if (hasSigningEnv) {
                signingConfig = signingConfigs.release
            }
        }
        release {
            if (hasSigningEnv) {
                signingConfig = signingConfigs.release
            }
        }
    }
    aaptOptions {
        ignoreAssetsPattern = '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['java']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['lib']
        }
    }
    packagingOptions{
      doNotStrip '**.setup'
      jniLibs {
          useLegacyPackaging = true
      }
    }
}

project.afterEvaluate {
    preBuild.dependsOn
}

dependencies {
    implementation 'androidx.core:core:1.16.0'
    implementation 'androidx.tvprovider:tvprovider:1.1.0'
    implementation 'com.google.code.gson:gson:2.13.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
